{-----------------------------------------------------------------------------
 Nome do Form: CTA01M03                                                Sergio
                                                                       Burini
 Espelho da apolice                                                  Nov/2006
-----------------------------------------------------------------------------}

database porto

screen size 25 by 80
{
 Atendimento...:[f         ]
--------------------------------------------------------------------cta01m03--
 Suc:[a1   ] Apl:[a2        ] Item:[a3      ] End:[a4       ][a5             ]
-----------------------------------+----------------------------Azul Seguros--
 Sucursal:[b1                     ]|            SEGURADO
 Emissao.:[b2        ]             |[c1                                      ]
 Vig.Apl.:[b3        ]a[b4        ]|[c2             ]  Fone:[c3              ]
 Convenio:[b5                     ]|
 Cobert..:[b6][b7                 ]|            CORRETOR
 Categ...:[b8]                     |[c4                                      ]
 Situacao:[b9       ]              |[c5      ]  Fone:[c6                     ]
-----------------------------------+------------------------------------------
 CLAUSULAS                         |              AUTO        [d0           ]
[e01][e02                         ]| Marca..:[d1              ]   [b11      ]
[e01][e02                         ]| Tipo...:[d2                             ]
[e01][e02                         ]| Modelo.:[d3                             ]
[e01][e02                         ]| Chassi.:[d4                  ]
[e01][e02                         ]| Placa..:[d5     ]
                                   | Fab/Mod:[d6  ]/[d7  ]   [d8           ]
-----------------------------------+------------------------------------------
}
end
tables gsakseg   ,
       gcakcorr  ,
       gcaksusep ,
       agbkmarca ,
       agbktip   ,
       agbkveic  ,
       abamapol  ,
       abamdoc   ,
       abbmveic  ,
       abbmcasco ,
       abbmdoc

attributes
    f  = formonly.atdnum  type dec(10,0), noentry, format = "<<<<<<<<&&";
   a1  = abamapol.succod                , noentry, format = "###&&";
   a2  = abamapol.aplnumdig             , noentry, format = "<<<<<<<<<&";
   a3  = abbmveic.itmnumdig             , noentry, format = "<<<<<<<&";
   a4  = abamdoc.edsnumref              , noentry, format = "<<<<<<<<&";
   a5  = formonly.solnom                , noentry;
   b1  = formonly.sucnom                , noentry;
   b2  = abamapol.emsdat                , noentry;
   b3  = abamapol.viginc                , noentry;
   b4  = abamapol.vigfnl                , noentry;
   b5  = formonly.cvnnom                , noentry;
   b6  = abbmcasco.cbtcod               , noentry, format = "&&";
   b7  = formonly.cbtdes                , noentry;
   b8  = abbmcasco.ctgtrfcod            , noentry, format = "&&";
   b9  = formonly.sitdes    type char   , noentry;
   c1  = gsakseg.segnom                 , noentry;
   c2  = formonly.cpfnumdig              , noentry;
   c3  = formonly.segteltxt type char   , noentry;

   c4  = gcakcorr.cornom                , noentry;
   c5  = gcaksusep.corsus               , noentry;
   c6  = formonly.corteltxt type char   , noentry;

   d0  = formonly.autrevtxt             , noentry;
   d1  = agbkmarca.vclmrcnom            , noentry;
   b11 = formonly.benef     type char   , noentry;
   d2  = agbktip.vcltipnom              , noentry;
   d3  = agbkveic.vclmdlnom             , noentry;
   d4  = formonly.vclchs    type char   , noentry;
   d5  = abbmveic.vcllicnum             , noentry;
   d6  = abbmveic.vclanofbc             , noentry;
   d7  = abbmveic.vclanomdl             , noentry;
   d8  = formonly.obs       type char   , noentry;
   e01 = formonly.clscod                , noentry;
   e02 = formonly.clsdes                , noentry;
end

instructions
   delimiters " "
   screen record s_cta01m03(clscod thru clsdes)
end
