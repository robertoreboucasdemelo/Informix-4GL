{-----------------------------------------------------------------------------
 Nome do Form: cta00m01                                               Marcelo

 Localiza apolices (Auto, R.E, Transportes)                        Nov/1994
 Alterado em 28/08/2001 por BenildaJMonteiro Psi - 132306
-----------------------------------------------------------------------------}

database porto
screen size 24 by 80
{
-------------------------------------------------------------------cta00m01--
                   Localiza apolices (Auto, R.E, Transportes)
-----------------------------------------------------------------------------
 Solicitante:[a              ]Avs.SinisTransp:[x]Tipo:[b |b1                ]
-------------------------- Chaves de Localizacao ----------------------------
 Protecao Patrimonial:[z]                                     [p           ] 
 Ramo.........:[c   |c1                                      ]    [q1  |q]
 Transportes..:[c3]              Consulta Averbacao Numero:[c4    ]
 Sucursal.....:[d    |d1                                      ]
 Apolice......:[e       ]        Item.:[f     ]
 Placa........:[g      ]
 Segurado.....:[j                                    ] Pessoa:[k]
 Cgc/Cpf......:[k1          ][k2  ][k3]         Certificado:[l              ]
 Chassi.......:[m        ]
 Vist.Previa:..[r]
 Sinistro.....:[s1  |s2    |s3  ]        Aviso Sin.F10......:[s4    |s5  ]  
 Vst.Sin.Auto.V10 V11.:[v1    |v2  ][v]  Vistoria Sin.RE.V12:[t1    |t2  ][t]
 Avs.Sin.Auto.N10 N11.:[u1    |u2  ][u]  Sem Docto..........:[y]         
 Proposta.....:[n |o       ]                                                    
}
end

 tables abamdoc  ,
        abbmveic ,
        gtakramo ,
        gsakseg  ,
        datksoltip

 attributes
    a  = formonly.solnom        , upshift
                                , comments = " Nome do solicitante";
    x  = formonly.flgavstransp  , upshift
                                , comments = " Marque (S) para Comunicar Sinistro de Transportes";
    b  = datksoltip.c24soltipcod, comments = " Tipo do solicitante";
    b1 = datksoltip.c24soltipdes, noentry;
    z  = formonly.ies_ppt       , upshift, autonext 
                                , comments = " Protecao Patrimonial? (S)im ou (N)ao";
    c  = gtakramo.ramcod        , autonext
                                , comments = " Localiza por apolice";
    c1 = gtakramo.ramnom        , noentry;
    c3 = formonly.transp        , autonext , upshift 
                                , comments = " Localiza apolice do Transportes(S/N)";   
    c4 = formonly.trpavbnum     , autonext   
                                , comments = " Digite o Numero da Averbacao";
    d  = formonly.succod          type decimal
                                , autonext
                                , comments = " Localiza por apolice";
    d1 = formonly.sucnom        , noentry;
    e  = formonly.aplnumdig       type decimal
                                , autonext
                                , comments = " Localiza por apolice";
    f  = formonly.itmnumdig       type decimal
                                , autonext
                                , comments = " Localiza por apolice";
    g  = abbmveic.vcllicnum     , upshift
                                , comments = " Localiza pela placa do veiculo";
    j  = formonly.segnom        , upshift
                                , comments = " Localiza pelo nome do segurado";
    k  = formonly.pestip        , upshift
                                , comments = " Tipo pessoa: (F)isica ou (J)uridica";

    k1 = gsakseg.cgccpfnum      , autonext
                                , comments = " Numero do CGC/CPF";
    k2 = gsakseg.cgcord         , autonext
                                , comments = " Numero da filial";
    k3 = gsakseg.cgccpfdig      , autonext
                                , comments = " Digito do CGC/CPF";
    l  = formonly.grevclctfnum  , upshift
                                , comments = " Localiza pelo certificado de garantia estendida";
    m  = abbmveic.vclchsfnl     , upshift
                                , comments = " Localiza pelo chassi do veiculo";
    n  = formonly.prporg          type decimal
                                , autonext
                                , comments = " Localiza pela proposta";
    o  = formonly.prpnumdig       type decimal
                                , comments = " Localiza pela proposta";
    y = formonly.semdocto      , upshift, comments = " Informe 'S' para ligacao sem docto";
    p  = formonly.c24paxtxt     , noentry;
    q1 = formonly.pactxt        , noentry;
    q  = formonly.pacflg        , upshift
                                , comments = " Localiza pelo PAC (Processo de Analise de Credito)? (S)im ou (N)ao";
    r  = formonly.vp            , upshift 
                                , comments = "Consulta por Vistoria Previa? (S)im ou (N)ao";
    v1 = formonly.sinautnum     , autonext
                                , comments = "Numero da Vistoria do Sin.Auto";
    v2 = formonly.sinautano     , comments = "Ano da Vistoria Sin.Auto"   ;
    v  = formonly.flgauto       , upshift,comments="Consulta por data?(S/N)";
    s1 = formonly.sinramcod     , comments = "Ramo do sinistro"           ;
    s2 = formonly.sinnum        , comments = "Numero do sinistro"         ;
    s3 = formonly.sinano        , comments = "Ano do sinistro"            ;
    s4 = formonly.sinvstnum     , autonext
                                , comments = "Numero do aviso de sinistro";
    s5 = formonly.sinvstano     , comments = "Ano do aviso de sinistro"   ;
    t1 = formonly.sinrenum      , autonext 
                                , comments = "Numero da Vistoria Sin.RE";
    t2 = formonly.sinreano      , comments = "Ano da Vistoria Sin.RE"   ;
    t  = formonly.flgre         , upshift,comments = "Consulta por data?(S/N)" ;
    u1 = formonly.sinavsnum     , autonext
                                , comments = "Numero do Aviso Sin. AUTO";
    u2 = formonly.sinavsano     , comments = "Ano do Aviso Sin.AUTO"   ;
    u  = formonly.flgavs        , upshift,comments = "Consulta por data?(S/N)" ;
 end

 instructions
    delimiters " "
 end
