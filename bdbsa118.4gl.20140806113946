#----------------------------------------------------------------------------#
# Porto Seguro Cia Seguros Gerais                                            #
#............................................................................#
# Sistema........: Porto Socorro                                             #
# Modulo.........: bdbsa118                                                  #
# Objetivo.......: O programa faz a leitura  da tabela de provisionamento e  #
#                  grava a baixa do provisionamento.                         #
# Analista Resp. : Norton Nery                                               #
# PSI            : 227340                                                    #
#............................................................................#
# Desenvolvimento: Thomas Ferrao - META                                      #
#............................................................................#
#                  * * *  ALTERACOES  * * *                                  #
#                                                                            #
# Data       Autor Fabrica PSI     Alteracao                                 #
# --------   ------------- ------  ------------------------------------------#
# 23/03/2010 Fabio Costa   198404  Trocar baixa de servicos nao provisionados#
#                                  por servicos nao pagos                    #
#----------------------------------------------------------------------------#
database porto

globals "/homedsa/projetos/dssqa/producao/I4GLParams.4gl"

define m_atddat date
     , m_prc  integer
     , m_bxa  integer
     , m_err  integer
        
main

   define l_path char(080)
        , l_log  char(080)

   let m_prc = 0
   let m_bxa = 0
   let m_err = 0
   
   call fun_dba_abre_banco('CT24HS')

   let l_path = f_path('DBS', 'LOG')

   if l_path is null or
      l_path = ' '   then
      let l_path = '.'
   end if

   let l_path = l_path clipped, "/dbs_bdbsa118.log"

   call startlog(l_path)

   let m_atddat = arg_val(1)

   ##  Verifica data recebida como parametro  ##
   if m_atddat is null or
      m_atddat = " "   then
       let m_atddat = today
   else
       if m_atddat > today then
          display "*** ERRO NO PARAMETRO: DATA INVALIDA! ***"
          call errorlog("*** ERRO NO PARAMETRO: DATA INVALIDA! ***")
          exit program(1)
       end if
   end if

   let l_log = 'Inicio do processamento: ',today,' as: ',time
   call errorlog(l_log)
   display l_log clipped

   display 'Arquivo: ', l_path clipped
   
   call bdbsa118_prepare()

   call bdbsa118_processamento()

   display "------------------------------------------------------------"
   display " RESUMO BDBSA118:  "
   display " Provisoes processadas......: ", m_prc using "#######&&"
   display " Provisoes baixa com sucesso: ", m_bxa using "#######&&"
   display " Provisoes baixa com erro...: ", m_err using "#######&&"
   display "------------------------------------------------------------"

   let l_log = 'Final  do processamento:',today,' as: ',time
   call errorlog(l_log)
   display l_log clipped

end main

#--------------------------------
function bdbsa118_prepare()
#--------------------------------

   define l_sql char(500)

   let l_sql = ' select  atdsrvnum, atdsrvano   '
              ,' from ctimsocprv                '
              ,' where extend(atdsrvabrdat, year to month) = extend(?, year to month) '
              ,' and prvtip    = "D"'
              ,' and prvmvttip = 1  '
   prepare pbdbsa118001 from l_sql
   declare cbdbsa118001 cursor for pbdbsa118001
   
   let l_sql =  'select  atdsrvnum, atdsrvano '
               ,'from  ctimsocprv             '
               ,'where atdsrvnum  = ?         '
               ,'and   atdsrvano  = ?         '
               ,'and   (   (prvtip = "C" and prvmvttip = 3) '
               ,      ' or (prvtip = "D" and prvmvttip = 4))'
   prepare pbdbsa118002 from l_sql
   declare cbdbsa118002 cursor for pbdbsa118002

   let l_sql = ' select atdsrvnum, pgtdat ',
               ' from datmservico    ',
               ' where atdsrvnum = ? ',
               '   and atdsrvano = ? '
   prepare p_srv_sel from l_sql
   declare c_srv_sel cursor for p_srv_sel
   
end function

#--------------------------------
function bdbsa118_processamento()
#--------------------------------
  define  l_erro      smallint
         ,l_msg   char (50)
         ,l_atdsrvabrdat date

  define lr_bdbsa118_1 record
      atdsrvnum like ctimsocprv.atdsrvnum
     ,atdsrvano like ctimsocprv.atdsrvano
  end record

  define lr_bdbsa118_2 record
      atdsrvnum like ctimsocprv.atdsrvnum
     ,atdsrvano like ctimsocprv.atdsrvano
  end record

  define lr_param  record
       evento                  char(06)
      ,empresa                 char(50)
      ,dt_movto                date
      ,chave_primaria          char(50)
      ,op                      char(50)
      ,apolice                 char(50)
      ,sucursal                char(50)
      ,projeto                 char(50)
      ,dt_chamado              date
      ,fvrcod                  char(50)
      ,fvrnom                  char(50)
      ,nfnum                   char(50)
      ,corsus                  char(50)
      ,cctnum                  char(50)
      ,modalidade              char(50)
      ,ramo                    char(50)
      ,opgvlr                  char(50)
      ,dt_vencto               date
      ,dt_ocorrencia           date
  end record

  define l_srvpgt record
         atdsrvnum  like datmservico.atdsrvnum, 
         pgtdat     like datmservico.pgtdat   
  end record
                   
  define l_dt_aux    char(10)
  define teste       char(1)

  let l_erro = null

  initialize lr_bdbsa118_1.* to null
  initialize lr_bdbsa118_2.* to null
  initialize l_srvpgt.* to null
  initialize lr_param.* to null
  initialize l_atdsrvabrdat to  null

  let l_atdsrvabrdat = m_atddat - 7 units month
  let l_dt_aux       =  year (l_atdsrvabrdat),"-", month(l_atdsrvabrdat) using "&&"

  open cbdbsa118001 using l_dt_aux

  foreach cbdbsa118001 into lr_bdbsa118_1.atdsrvnum
                           ,lr_bdbsa118_1.atdsrvano
                           
     initialize l_srvpgt.* to null
     
     let m_prc = m_prc + 1
   
     open cbdbsa118002 using lr_bdbsa118_1.atdsrvnum
                            ,lr_bdbsa118_1.atdsrvano

     whenever error continue
     fetch cbdbsa118002 into  lr_bdbsa118_2.atdsrvnum
                             ,lr_bdbsa118_2.atdsrvano

     whenever error stop

     if sqlca.sqlcode < 0  then
        let l_msg = 'Erro SELECT cbdbsa118002: ',sqlca.sqlcode,' | ',sqlca.sqlerrd[2]
        call errorlog(l_msg)
        let l_msg = 'bdbsa118/ bdbsa118_processamento() / '
        call errorlog(l_msg)
        exit program(1)
     end if

     if lr_bdbsa118_1.atdsrvnum = lr_bdbsa118_2.atdsrvnum and
        lr_bdbsa118_1.atdsrvano = lr_bdbsa118_2.atdsrvano 
        then
        continue foreach
     else

        whenever error continue
        open c_srv_sel using lr_bdbsa118_1.atdsrvnum, lr_bdbsa118_1.atdsrvano
        fetch c_srv_sel into l_srvpgt.atdsrvnum, l_srvpgt.pgtdat
        whenever error stop
        
        if sqlca.sqlcode != 0
           then
           continue foreach
        end if
        
        if l_srvpgt.pgtdat is not null  # servico pago nao fazer baixa
           then
           continue foreach
        end if
        
        display 'Servico: ', l_srvpgt.atdsrvnum
        
        call ctb00g03_bxaprvdsp(lr_bdbsa118_1.atdsrvnum, lr_bdbsa118_1.atdsrvano)
             returning l_erro
                      ,lr_param.evento
                      ,lr_param.empresa
                      ,lr_param.dt_movto
                      ,lr_param.chave_primaria
                      ,lr_param.op
                      ,lr_param.apolice
                      ,lr_param.sucursal
                      ,lr_param.projeto
                      ,lr_param.dt_chamado
                      ,lr_param.fvrcod
                      ,lr_param.fvrnom
                      ,lr_param.nfnum
                      ,lr_param.corsus
                      ,lr_param.cctnum
                      ,lr_param.modalidade
                      ,lr_param.ramo
                      ,lr_param.opgvlr
                      ,lr_param.dt_vencto
                      ,lr_param.dt_ocorrencia
 
        if l_erro <> 0 
           then
           display 'Erro: ', l_erro
           call errorlog(l_erro)
           let m_err = m_err + 1
        else
           let m_bxa = m_bxa + 1
        end if
     end if
     
  end foreach

end function
